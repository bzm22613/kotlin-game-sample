group 'work.katashin'
version '0.0.1-SNAPSHOT'

buildscript {
    ext.kotlin_version = "1.1.51"

    repositories {
        mavenLocal()
        jcenter()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.21"
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

repositories {
    mavenLocal()
    jcenter()
    maven { url "http://dl.bintray.com/kotlin/kotlinx.html" }
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/squash" }
}

dependencies {
    def html_version = "0.6.4"

    compile("org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version")
    compile("org.jetbrains.kotlin:kotlin-test-js:$kotlin_version")
    compile("org.jetbrains.kotlinx:kotlinx-html-js:$html_version")
}

kotlinFrontend {

    sourceMaps = false

    npm {
        replaceVersion("kotlinx-html-js", "1.1.0")
        replaceVersion("kotlinx-html-shared", "1.1.0")
        replaceVersion("kotlin-js-library", "1.1.0")

        dependency("react")
        dependency("react-dom")
        dependency("react-router")
        dependency("jquery")
        dependency("react-markdown")

        devDependency("css-loader")
        devDependency("style-loader")
        devDependency("babel-loader")
        devDependency("babel-core")
        devDependency("babel-preset-es2015")
        devDependency("karma")
        devDependency("html-webpack-plugin")
    }

    webpackBundle {
        port = 3000
        contentPath = file('public')
        publicPath = "/"
        bundleName = "main"

    }

}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

sourceSets {
    main.kotlin.srcDirs += "src"
    main.resources.srcDirs += "resources"
    main.output.resourcesDir = "build/js/resources"
    test.kotlin.srcDirs += "test"
    test.resources.srcDirs += "testResources"
}

task copyResources(type: Copy) {
    from sourceSets.main.resources.srcDirs
    into file(buildDir.path + "/js")
}

afterEvaluate {
    tasks.getByName("webpack-bundle") { dependsOn(copyResources) }
    tasks.getByName("webpack-run") { dependsOn(copyResources) }
}
